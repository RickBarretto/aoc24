;; module: "AOC24 - Day 01"
;; author: "RickBarretto" 
;; license: {
;;		This Source Code Form is subject to the terms of the Mozilla Public
;;  	License, v. 2.0. If a copy of the MPL was not distributed with this
;; 		file, You can obtain one at https://mozilla.org/MPL/2.0/.
;; }

distance: $[a :integer b :integer][
    abs (a - b)
]

distances: $[left right][
    ensure.that: "can be properly coupled" 
        -> equal? size left size right
    
    couple left right
        | map [pair] -> distance pair\0 pair\1
]

resultFrom: $[file :string][
    file: read.lines file 

    [left right]: to [:block[:integer]] file 
        | map => [split.by: " " &]
        | map => [remove & ""]
        | decouple
        | map => [sort &]

    sum distances left right
]

if standalone? [
	inspect resultFrom ./"../data/sample.txt"
]